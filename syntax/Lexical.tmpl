lexical syntax // Layout & Comments

	[\ \t\n\r\12]                   -> LAYOUT
	"/*" ( ~[\*] | Asterisk )* "*/" -> Comment
	Comment                         -> LAYOUT
	[\*]                            -> Asterisk
	"//" ~[\n]* [\n]                -> Comment
	
	-> EOF

lexical restrictions

	Asterisk -/- [\/]
	EOF      -/- ~[]

context-free restrictions
  
	LAYOUT? -/- [\ \t\n\r\12]
	LAYOUT? -/- [\/].[\/]
	LAYOUT? -/- [\/].[\*]
	

lexical syntax // Common

	[\,]   -> Comma
	[\+\-] -> Sign
	

lexical syntax // Identifiers

	([a-zA-Z\_]|UniCharName) ([a-zA-Z\_0-9]|UniCharName)* -> Id
	Keyword                                               -> Id {reject}
	
lexical restrictions
  
	Id -/- [a-zA-Z\_0-9]

	
templates // Constants

	Constant.CharConst   = <<CharConst>>
	Constant.IntConst    = <<IntConst>>
	Constant.OctConst    = <<OctConst>>
	Constant.HexConst    = <<HexConst>>
	Constant.FloatConst  = <<FloatConst>>
	Constant.StringConst = <<StringConst>>
	

lexical syntax // Integer constant

	// TODO: IntSuffix should be in AST.
	NonzeroDigit Digit* IntSuffix? -> IntConst
	[0] IntSuffix?                 -> IntConst
	
	UnsingedSuffix LongSuffix?    -> IntSuffix
	UnsignedSuffix LongLongSuffix -> IntSuffix
	LongSuffix UnsigedSuffix?     -> IntSuffix
	LongLongSuffix UnsigedSuffix? -> IntSuffix
	
	[uU] -> UnsignedSuffix
	[lL] -> LongSuffix
	"ll" -> LongLongSuffix
	"LL" -> LongLongSuffix
	
	[0-9] -> Digit
	[1-9] -> NonzeroDigit
	
lexical restrictions

	IntConst -/- [0-9ulUL]


lexical syntax // Octal constant

	[0] OctDigit+ -> OctConst
	[0-7]         -> OctDigit
	
lexical restrictions

	OctConst -/- [0-7]

	
lexical syntax // Hexadecimal constants

	[0] HexPrefix HexDigit+ -> HexConst
	[Xx]                    -> HexPrefix
	[0-9a-fA-F]             -> HexDigit

lexical restrictions

	HexConst  -/- [0-9a-fA-FxX]
	

lexical syntax // Float constants

	DecFloatConst -> FloatConst
	HexFloatConst -> FloatConst
	
	// TODO: ExpPart & FloatSuffix should be in AST.
	FracConst ExpPart? FloatSuffix? -> DecFloatConst
	Digit+    ExpPart FloatSuffix?  -> DecFloatConst
	
	Digit* "." Digit+ -> FracConst
	Digit+ "."        -> FracConst
	
	// TODO: Sign should be in AST.
	[eE] Sign? Digit+ -> ExpPart
	[pP] Sign? Digit+ -> BinExpPart
	
	// TODO: BinExpPart & FloatSuffix should be in AST.
	HexPrefix HexFracConst BinExpPart FloatSuffix? -> HexFloatConst
	HexPrefix HexDigit+    BinExpPart FloatSuffix? -> HexFloatConst
	
	HexDigit* "." HexDigit+ -> HexFracConst
	HexDigit+ "."           -> HexFracConst
	
	[flFL] -> FloatSuffix
	
lexical restrictions

	FloatConst -/- [0-9a-fA-FxXflFL]


lexical syntax // Universal character names

	"\\u" HexQuad                       -> UniCharName
	"\\U" HexQuad HexQuad               -> UniCharName
	HexDigit HexDigit HexDigit HexDigit -> HexQuad


lexical syntax // Escape sequences
	
	[\\] [\'\"\?\\abfnrtv]            -> EscSeq
	[\\] OctDigit OctDigit? OctDigit? -> EscSeq
	[\\] HexPrefix HexDigit+          -> EscSeq
	UniCharName                       -> EscSeq
	

lexical syntax // Character constant
	
	// TODO: CharPrefix should be in AST.
	[LuU]? "'" Char+ "'" -> CharConst
	~[\\\'\n]            -> Char
	EscSeq               -> Char


lexical syntax // String constant

	// TODO: EncPrefix should be in AST.
	EncPrefix? "\"" SChar* "\"" -> StringConst
	"u8"                        -> EncPrefix
	[uUL]                       -> EncPrefix
	~[\\\"\n]                   -> SChar
	EscSeq                      -> SChar
	