module names

imports

	include/C
	
namespaces

	Struct
	Union
	Field
	Declaration
	Function

properties

	decl-specs of Field : List(DeclSpec)
	decl-specs of Function : List(DeclSpec)
	decl-specs of Declaration : List(DeclSpec)

binding rules

	TranslationUnit(_):
		scopes Struct, Union, Function, Declaration
		implicitly defines Function "printf"
		implicitly defines Function "puts"
		implicitly defines Function "atoi"

	StructSpec(Some(s)):
		defines Struct s 
			// in subsequent scope
		
	StructSpec(Some(s), _):
		defines Struct s 
			// in subsequent scope
		scopes Field
		
	StructSpec(None(), _):
		scopes Field
	
	FieldDecl(ds*, Declarator(_, f)):
		defines Field f
			of decl-specs ds*
			
	FuncDef(ds*, Declarator(_, FuncDeclarator(f, _)), _, _):
		defines Function f
			of decl-specs ds*
				// in subsequent scope
		scopes Declaration

	ParamDecl(ds*, Declarator(ptr, d)):
		defines Declaration d
			of decl-specs ds*
			// in subsequent scope
		
	Decl(ds*, Declarator(ptr, d)):
		defines Declaration d
			of decl-specs ds*
			// in subsequent scope
			
	DeclRef(d):
		refers to Declaration d
		otherwise
			refers to Struct d
		otherwise
			refers to Function d
		